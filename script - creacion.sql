use GD2C2017
go

/*Se eliminan objetos existentes*/

IF OBJECT_ID('POLACA_INVERSA.ROL_ACCESOS','U') IS NOT NULL
    DROP TABLE POLACA_INVERSA.ROL_ACCESOS;

IF OBJECT_ID('POLACA_INVERSA.ACCESOS','U') IS NOT NULL
    DROP TABLE POLACA_INVERSA.ACCESOS;

IF OBJECT_ID('POLACA_INVERSA.ROL_USUARIO','U') IS NOT NULL
    DROP TABLE POLACA_INVERSA.ROL_USUARIO;

IF OBJECT_ID('POLACA_INVERSA.ROLES','U') IS NOT NULL
    DROP TABLE POLACA_INVERSA.ROLES;

IF OBJECT_ID('POLACA_INVERSA.SUCURSAL_USUARIO','U') IS NOT NULL
    DROP TABLE POLACA_INVERSA.SUCURSAL_USUARIO;

IF OBJECT_ID('POLACA_INVERSA.USUARIOS','U') IS NOT NULL
    DROP TABLE POLACA_INVERSA.USUARIOS;

IF OBJECT_ID('POLACA_INVERSA.ITEMS_DEVOLUCIONES','U') IS NOT NULL
	DROP TABLE POLACA_INVERSA.ITEMS_DEVOLUCIONES;

IF OBJECT_ID('POLACA_INVERSA.DEVOLUCIONES','U') IS NOT NULL
	DROP TABLE POLACA_INVERSA.DEVOLUCIONES;

IF OBJECT_ID('POLACA_INVERSA.ITEMS_FACTURAS_RENDIDAS','U') IS NOT NULL
	DROP TABLE POLACA_INVERSA.ITEMS_FACTURAS_RENDIDAS;
	
IF OBJECT_ID('POLACA_INVERSA.FACTURAS_RENDIDAS','U') IS NOT NULL
	DROP TABLE POLACA_INVERSA.FACTURAS_RENDIDAS;
	
IF OBJECT_ID('POLACA_INVERSA.ITEMS_FACTURAS','U') IS NOT NULL
	DROP TABLE POLACA_INVERSA.ITEMS_FACTURAS;

IF OBJECT_ID('POLACA_INVERSA.ITEMS_PAGOS','U') IS NOT NULL
	DROP TABLE POLACA_INVERSA.ITEMS_PAGOS;
	
IF OBJECT_ID('POLACA_INVERSA.FACTURAS','U') IS NOT NULL
    DROP TABLE POLACA_INVERSA.FACTURAS;

IF OBJECT_ID('POLACA_INVERSA.PAGOS','U') IS NOT NULL
    DROP TABLE POLACA_INVERSA.PAGOS;
	
IF OBJECT_ID('POLACA_INVERSA.MEDIOS_PAGO','U') IS NOT NULL
    DROP TABLE POLACA_INVERSA.MEDIOS_PAGO;

IF OBJECT_ID('POLACA_INVERSA.SUCURSALES','U') IS NOT NULL
    DROP TABLE POLACA_INVERSA.SUCURSALES;
	
IF OBJECT_ID('POLACA_INVERSA.EMPRESAS','U') IS NOT NULL
	DROP TABLE POLACA_INVERSA.EMPRESAS;
	
IF OBJECT_ID('POLACA_INVERSA.RUBRO','U') IS NOT NULL
	DROP TABLE POLACA_INVERSA.RUBRO;

IF OBJECT_ID('POLACA_INVERSA.CLIENTES','U') IS NOT NULL
    DROP TABLE POLACA_INVERSA.CLIENTES;

IF OBJECT_ID('POLACA_INVERSA.SPLOGIN') IS NOT NULL
    DROP PROCEDURE POLACA_INVERSA.SPLOGIN;

IF OBJECT_ID('POLACA_INVERSA._INHABILITAR') IS NOT NULL
    DROP PROCEDURE POLACA_INVERSA._INHABILITAR;	
	
IF OBJECT_ID('POLACA_INVERSA._HABILITAR') IS NOT NULL
    DROP PROCEDURE POLACA_INVERSA._HABILITAR;

IF OBJECT_ID('POLACA_INVERSA.CLIENTE_UPDATE') IS NOT NULL
    DROP PROCEDURE POLACA_INVERSA.CLIENTE_UPDATE;	

IF OBJECT_ID('POLACA_INVERSA.CLIENTE_NUEVO') IS NOT NULL
	DROP PROCEDURE POLACA_INVERSA.CLIENTE_NUEVO;
	
IF OBJECT_ID('POLACA_INVERSA.USUARIO_GET_ID') IS NOT NULL
    DROP FUNCTION POLACA_INVERSA.USUARIO_GET_ID;

IF OBJECT_ID('POLACA_INVERSA.GET_MEDIOS_PAGOS') IS NOT NULL
    DROP FUNCTION POLACA_INVERSA.GET_MEDIOS_PAGOS;	
	
IF OBJECT_ID('POLACA_INVERSA.ROL_UPDATE') IS NOT NULL
    DROP PROCEDURE POLACA_INVERSA.ROL_UPDATE;

IF OBJECT_ID('POLACA_INVERSA.ROL_NUEVO') IS NOT NULL
    DROP PROCEDURE POLACA_INVERSA.ROL_NUEVO;	

IF OBJECT_ID('POLACA_INVERSA.ROL_INHABILITAR') IS NOT NULL
    DROP PROCEDURE POLACA_INVERSA.ROL_INHABILITAR;

IF OBJECT_ID('POLACA_INVERSA.RENDIR_FACTURAS') IS NOT NULL
    DROP PROCEDURE POLACA_INVERSA.RENDIR_FACTURAS;
	
IF OBJECT_ID('POLACA_INVERSA.USUARIO_GET_ROLES') IS NOT NULL
    DROP FUNCTION POLACA_INVERSA.USUARIO_GET_ROLES;

IF OBJECT_ID('POLACA_INVERSA.USUARIO_GET_SUCURSALES') IS NOT NULL
    DROP FUNCTION POLACA_INVERSA.USUARIO_GET_SUCURSALES;
	
IF OBJECT_ID('POLACA_INVERSA.ROL_GET_ACCESOS') IS NOT NULL
    DROP FUNCTION POLACA_INVERSA.ROL_GET_ACCESOS;

IF OBJECT_ID('POLACA_INVERSA.ACCESO_GET_NOMBRE') IS NOT NULL
    DROP FUNCTION POLACA_INVERSA.ACCESO_GET_NOMBRE;	

IF OBJECT_ID('POLACA_INVERSA.GET_CLIENTES_CON_FILTROS') IS NOT NULL
    DROP FUNCTION POLACA_INVERSA.GET_CLIENTES_CON_FILTROS;

IF OBJECT_ID('POLACA_INVERSA.FUNCIONALIDADES_GET_TABLA_DE_ROL') IS NOT NULL
    DROP FUNCTION POLACA_INVERSA.FUNCIONALIDADES_GET_TABLA_DE_ROL;	

IF OBJECT_ID('POLACA_INVERSA.FUNCIONALIDADES_GET_TABLA_DE_ROL_DISPONIBLES') IS NOT NULL
    DROP FUNCTION POLACA_INVERSA.FUNCIONALIDADES_GET_TABLA_DE_ROL_DISPONIBLES;	

IF OBJECT_ID('POLACA_INVERSA.GET_EMPRESAS_CON_FILTROS') IS NOT NULL
    DROP FUNCTION POLACA_INVERSA.GET_EMPRESAS_CON_FILTROS;	

IF OBJECT_ID('POLACA_INVERSA.GET_FACTURAS_CON_FILTROS') IS NOT NULL
    DROP FUNCTION POLACA_INVERSA.GET_FACTURAS_CON_FILTROS;

IF OBJECT_ID('POLACA_INVERSA.FACTURA_ESTACOBRADA') IS NOT NULL
    DROP FUNCTION POLACA_INVERSA.FACTURA_ESTACOBRADA;

IF OBJECT_ID('POLACA_INVERSA.FACTURA_GET_DETALLE') IS NOT NULL
    DROP FUNCTION POLACA_INVERSA.FACTURA_GET_DETALLE;

IF OBJECT_ID('POLACA_INVERSA.GET_SUCURSAL_CON_FILTROS') IS NOT NULL
    DROP FUNCTION POLACA_INVERSA.GET_SUCURSAL_CON_FILTROS;

IF OBJECT_ID('POLACA_INVERSA.SUCURSAL_ESTA_HABILITADA') IS NOT NULL
    DROP FUNCTION POLACA_INVERSA.SUCURSAL_ESTA_HABILITADA;

IF OBJECT_ID('POLACA_INVERSA.BUSCAR_FACTURAS_PENDIENTES_DE_RENDICION_EMPRESA') IS NOT NULL
    DROP FUNCTION POLACA_INVERSA.BUSCAR_FACTURAS_PENDIENTES_DE_RENDICION_EMPRESA;	

IF OBJECT_ID('POLACA_INVERSA.FACTURA_EXISTE') IS NOT NULL
    DROP FUNCTION POLACA_INVERSA.FACTURA_EXISTE;	
	
IF OBJECT_ID('POLACA_INVERSA.FACTURA_ESTA_HABILITADA') IS NOT NULL
    DROP FUNCTION POLACA_INVERSA.FACTURA_ESTA_HABILITADA;	

IF OBJECT_ID('POLACA_INVERSA.FACTURA_ES_DE_EMPRESA') IS NOT NULL
    DROP FUNCTION POLACA_INVERSA.FACTURA_ES_DE_EMPRESA;	

IF OBJECT_ID('POLACA_INVERSA.FACTURA_ES_DE_CLIENTE') IS NOT NULL
    DROP FUNCTION POLACA_INVERSA.FACTURA_ES_DE_CLIENTE;	

IF OBJECT_ID('POLACA_INVERSA.GET_FECHA_VENCIMIENTO_FACTURA') IS NOT NULL
    DROP FUNCTION POLACA_INVERSA.GET_FECHA_VENCIMIENTO_FACTURA;		

IF OBJECT_ID('POLACA_INVERSA.FACTURA_VALIDAR_IMPORTE') IS NOT NULL
    DROP FUNCTION POLACA_INVERSA.FACTURA_VALIDAR_IMPORTE;

IF OBJECT_ID('POLACA_INVERSA.BUSCAR_FACTURAS_A_RENDIR') IS NOT NULL
    DROP FUNCTION POLACA_INVERSA.BUSCAR_FACTURAS_A_RENDIR;	

IF OBJECT_ID('POLACA_INVERSA.VALIDAR_RENDICION_FECHAxEMPRESA') IS NOT NULL
    DROP FUNCTION POLACA_INVERSA.VALIDAR_RENDICION_FECHAxEMPRESA;

IF OBJECT_ID('POLACA_INVERSA.GET_FACTURAS_PAGAS_CON_FILTROS') IS NOT NULL
    DROP FUNCTION POLACA_INVERSA.GET_FACTURAS_PAGAS_CON_FILTROS;

IF OBJECT_ID('POLACA_INVERSA.ES_EMAIL_EXISTENTE') IS NOT NULL
    DROP FUNCTION POLACA_INVERSA.ES_EMAIL_EXISTENTE;	
	
IF OBJECT_ID('POLACA_INVERSA.SUCURSAL_UPDATE') IS NOT NULL
    DROP PROCEDURE POLACA_INVERSA.SUCURSAL_UPDATE;
	
IF OBJECT_ID('POLACA_INVERSA.EMPRESA_UPDATE') IS NOT NULL
    DROP PROCEDURE POLACA_INVERSA.EMPRESA_UPDATE;

IF OBJECT_ID('POLACA_INVERSA.EMPRESA_NUEVA') IS NOT NULL
	DROP PROCEDURE POLACA_INVERSA.EMPRESA_NUEVA;
	
IF OBJECT_ID('POLACA_INVERSA.HABILITAR_EMPRESA') IS NOT NULL
    DROP PROCEDURE POLACA_INVERSA.HABILITAR_EMPRESA;
	
IF OBJECT_ID('POLACA_INVERSA.INHABILITAR_EMPRESA') IS NOT NULL
    DROP PROCEDURE POLACA_INVERSA.INHABILITAR_EMPRESA;

IF OBJECT_ID('POLACA_INVERSA.FACTURA_NUEVA') IS NOT NULL
    DROP PROCEDURE POLACA_INVERSA.FACTURA_NUEVA;

IF OBJECT_ID('POLACA_INVERSA.DETALLE_FACTURA_NUEVO') IS NOT NULL
    DROP PROCEDURE POLACA_INVERSA.DETALLE_FACTURA_NUEVO;

IF OBJECT_ID('POLACA_INVERSA.FACTURA_UPDATE') IS NOT NULL
    DROP PROCEDURE POLACA_INVERSA.FACTURA_UPDATE;

IF OBJECT_ID('POLACA_INVERSA.INHABILITAR_FACTURA') IS NOT NULL
    DROP PROCEDURE POLACA_INVERSA.INHABILITAR_FACTURA;

IF OBJECT_ID('POLACA_INVERSA.SUCURSAL_NUEVO') IS NOT NULL
    DROP PROCEDURE POLACA_INVERSA.SUCURSAL_NUEVO;

IF OBJECT_ID('POLACA_INVERSA.INHABILITAR_SUCURSAL') IS NOT NULL
    DROP PROCEDURE POLACA_INVERSA.INHABILITAR_SUCURSAL;
	
IF OBJECT_ID('POLACA_INVERSA.HABILITAR_SUCURSAL') IS NOT NULL
    DROP PROCEDURE POLACA_INVERSA.HABILITAR_SUCURSAL;

IF OBJECT_ID('POLACA_INVERSA.PAGO_NUEVO') IS NOT NULL
    DROP PROCEDURE POLACA_INVERSA.PAGO_NUEVO;

IF OBJECT_ID('POLACA_INVERSA.DEVOLUCION_NUEVA') IS NOT NULL
    DROP PROCEDURE POLACA_INVERSA.DEVOLUCION_NUEVA;	
	
IF OBJECT_ID('POLACA_INVERSA.MigrarClientes') IS NOT NULL
    DROP PROCEDURE POLACA_INVERSA.MigrarClientes;
	
IF OBJECT_ID('POLACA_INVERSA.MigrarRubros') IS NOT NULL
    DROP PROCEDURE POLACA_INVERSA.MigrarRubros;
	
IF OBJECT_ID('POLACA_INVERSA.MigrarEmpresas') IS NOT NULL
    DROP PROCEDURE POLACA_INVERSA.MigrarEmpresas;
	
IF OBJECT_ID('POLACA_INVERSA.MigrarItemFactura') IS NOT NULL
    DROP PROCEDURE POLACA_INVERSA.MigrarItemFactura;

IF OBJECT_ID('POLACA_INVERSA.MigrarFactura') IS NOT NULL
    DROP PROCEDURE POLACA_INVERSA.MigrarFactura;
	
IF OBJECT_ID('POLACA_INVERSA.MigrarSucursales') IS NOT NULL
    DROP PROCEDURE POLACA_INVERSA.MigrarSucursales;

IF OBJECT_ID('POLACA_INVERSA.MigrarPagos') IS NOT NULL
    DROP PROCEDURE POLACA_INVERSA.MigrarPagos;

IF OBJECT_ID('POLACA_INVERSA.MigrarMediosDePagos') IS NOT NULL
    DROP PROCEDURE POLACA_INVERSA.MigrarMediosDePagos;	

IF OBJECT_ID('POLACA_INVERSA.MigrarRendicion') IS NOT NULL
    DROP PROCEDURE POLACA_INVERSA.MigrarRendicion;	
	
IF TYPE_ID(N'POLACA_INVERSA.TABLA_ROL_X_ACCESO') IS NOT NULL
	DROP TYPE POLACA_INVERSA.TABLA_ROL_X_ACCESO;
	
IF TYPE_ID(N'POLACA_INVERSA.TABLA_ITEMS_FACTURA') IS NOT NULL
	DROP TYPE POLACA_INVERSA.TABLA_ITEMS_FACTURA;
	
IF TYPE_ID(N'POLACA_INVERSA.TABLA_ITEMS_PAGO') IS NOT NULL
	DROP TYPE POLACA_INVERSA.TABLA_ITEMS_PAGO;

IF TYPE_ID(N'POLACA_INVERSA.TABLA_ITEMS_RENDICION') IS NOT NULL
	DROP TYPE POLACA_INVERSA.TABLA_ITEMS_RENDICION;	

IF TYPE_ID(N'POLACA_INVERSA.TABLA_ITEMS_DEVOLUCION') IS NOT NULL
	DROP TYPE POLACA_INVERSA.TABLA_ITEMS_DEVOLUCION;		
	
	
IF EXISTS (SELECT SCHEMA_NAME FROM INFORMATION_SCHEMA.SCHEMATA WHERE SCHEMA_NAME = 'POLACA_INVERSA')
    DROP SCHEMA POLACA_INVERSA
GO
	
/*CREO NUEVO ESQUEMA */

IF NOT EXISTS (SELECT * FROM sys.schemas WHERE name = 'POLACA_INVERSA')
  BEGIN
    EXEC ('CREATE SCHEMA POLACA_INVERSA;');
  END;

/*CREO LA TABLA DE USUARIOS */

CREATE TABLE [POLACA_INVERSA].USUARIOS(
	ID_USUARIO INT PRIMARY KEY IDENTITY(1,1),
	USERNAME NVARCHAR(20) UNIQUE NOT NULL,
	PASSWORD VARBINARY(250) NOT NULL,
	HABILITADO BIT NOT NULL,
	INTENTOS TINYINT NOT NULL
);
GO
/*CREO LA TABLA DE ROLES */
CREATE TABLE [POLACA_INVERSA].ROLES (
	ID_ROL TINYINT IDENTITY(1,1) PRIMARY KEY,
	NOMBRE_ROL VARCHAR(250) UNIQUE,
	HABILITADO BIT NOT NULL
);
GO
/*CREO LA TABLA DE ROLES POR USUARIO*/
CREATE TABLE [POLACA_INVERSA].ROL_USUARIO (
	ID_USUARIO INT FOREIGN KEY REFERENCES POLACA_INVERSA.USUARIOS(Id_Usuario),
	ID_ROL TINYINT FOREIGN KEY REFERENCES POLACA_INVERSA.ROLES(Id_Rol),
	PRIMARY KEY(ID_USUARIO,ID_ROL)
);
GO
/*CREO LA TABLA DE ACCESOS*/
CREATE TABLE [POLACA_INVERSA].ACCESOS (
	ID_ACCESO TINYINT IDENTITY(1,1) PRIMARY KEY,
	NOMBRE VARCHAR(100) UNIQUE,
);
GO
/*CREO LA TABLA DE ROL POR ACCESOS*/
CREATE TABLE [POLACA_INVERSA].ROL_ACCESOS (
	ID_ROL TINYINT FOREIGN KEY REFERENCES POLACA_INVERSA.ROLES(Id_Rol),
	ID_ACCESO TINYINT FOREIGN KEY REFERENCES POLACA_INVERSA.ACCESOS(Id_Acceso),
	PRIMARY KEY (ID_ROL,ID_ACCESO)
);
GO
/*CREO LA TABLA RUBRO*/
CREATE TABLE [POLACA_INVERSA].RUBRO(
	ID_RUBRO INT PRIMARY KEY,
	DETALLE VARCHAR(250) NOT NULL,
)
GO
/*CREO LA TABLA DE EMPRESAS*/
CREATE TABLE [POLACA_INVERSA].EMPRESAS(
	ID_EMPRESA INT PRIMARY KEY IDENTITY(1,1),
	CUIT VARCHAR(50) UNIQUE NOT NULL,
	ID_RUBRO INT FOREIGN KEY REFERENCES [POLACA_INVERSA].RUBRO(ID_RUBRO),
	NOMBRE VARCHAR(250) NOT NULL,
	DIRECCION VARCHAR(250) NOT NULL,
	ESTADO_EMPRESA BIT NOT NULL Default 1,
)
GO
/*CREO LA TABLA DE CLIENTES*/
CREATE TABLE [POLACA_INVERSA].CLIENTES (
	ID_CLIENTE INT PRIMARY KEY IDENTITY(1,1),
	DNI VARCHAR(20) UNIQUE NOT NULL,
	NOMBRE VARCHAR(250) NOT NULL,
	APELLIDO VARCHAR(250) NOT NULL,
	MAIL VARCHAR(100) NOT NULL,
	TELEFONO VARCHAR(20) NOT NULL DEFAULT '0',
	DIRECCION VARCHAR(100) NOT NULL,
	NUMERO INT NOT NULL,
	PISO INT NOT NULL,
	DPTO VARCHAR(10) NULL,
	LOCALIDAD VARCHAR (50) NOT NULL,
	CODIGO_POSTAL INT NOT NULL,
	FECHA_NACIMIENTO DATETIME NOT NULL,
	HABILITADO BIT NOT NULL Default 1
);
GO
/*CREO LA TABLA DE FACTURAS */
CREATE TABLE [POLACA_INVERSA].FACTURAS (
	ID_FACTURA NUMERIC(18,0) PRIMARY KEY,
	ID_EMPRESA INT NOT NULL FOREIGN KEY REFERENCES POLACA_INVERSA.EMPRESAS(ID_EMPRESA),
	ID_CLIENTE INT NOT NULL FOREIGN KEY REFERENCES POLACA_INVERSA.CLIENTES(ID_CLIENTE),
	FECHA_ALTA DATETIME,
	FECHA_VENCIMIENTO DATETIME,
	TOTAL NUMERIC(18,2),
	HABILITADO BIT NOT NULL Default 1
);
GO
/*CREO LA TABLA DE SUCURSALES*/
CREATE TABLE [POLACA_INVERSA].SUCURSALES (
	ID_SUCURSAL INT IDENTITY(1,1) PRIMARY KEY,
	NOMBRE VARCHAR(250) NOT NULL,
	DIRECCION VARCHAR(250) NOT NULL,
	CODIGO_POSTAL INT UNIQUE NOT NULL,
	ESTADO_SUCURSAL BIT NOT NULL
);
GO
/*CREO LA TABLA MEDIOS DE PAGOS*/
CREATE TABLE [POLACA_INVERSA].MEDIOS_PAGO (
	ID_MEDIO INT PRIMARY KEY IDENTITY,
	DESCRIPCION VARCHAR(20) NOT NULL
);
GO
/*CREO LA TABLA DE PAGOS */
CREATE TABLE [POLACA_INVERSA].PAGOS (
	ID_PAGO INT PRIMARY KEY,
	ID_SUCURSAL INT NOT NULL FOREIGN KEY REFERENCES POLACA_INVERSA.SUCURSALES(ID_SUCURSAL),
	ID_CLIENTE INT NOT NULL FOREIGN KEY REFERENCES POLACA_INVERSA.CLIENTES(ID_CLIENTE),
	MEDIO_PAGO INT NOT NULL FOREIGN KEY REFERENCES POLACA_INVERSA.MEDIOS_PAGO(ID_MEDIO),
	FECHA_PAGO DATETIME NOT NULL,
	TOTAL_PAGO NUMERIC(18,2) NOT NULL
);
GO
/*ITEM DE PAGO*/
CREATE TABLE [POLACA_INVERSA].ITEMS_PAGOS (
	ID_PAGO INTEGER NOT NULL FOREIGN KEY REFERENCES POLACA_INVERSA.PAGOS(ID_PAGO),
	ID_FACTURA NUMERIC(18,0) NOT NULL FOREIGN KEY REFERENCES POLACA_INVERSA.FACTURAS(ID_FACTURA)
);
GO
/*CREO LA TABLA DE SUCURSAL POR USUARIO*/
CREATE TABLE [POLACA_INVERSA].SUCURSAL_USUARIO (
	ID_USUARIO INT FOREIGN KEY REFERENCES [POLACA_INVERSA].USUARIOS(ID_USUARIO),
	ID_SUCURSAL INT FOREIGN KEY REFERENCES [POLACA_INVERSA].SUCURSALES(ID_SUCURSAL),
	PRIMARY KEY(ID_USUARIO,ID_SUCURSAL)
);
GO
/*CREO LA TABLA DE DEVOLUCIONES*/
CREATE TABLE [POLACA_INVERSA].DEVOLUCIONES(
	ID_DEVOLUCION INT PRIMARY KEY,
	FECHA_DEVOLUCION DATETIME NOT NULL,
	MOTIVO_DEVOLUCION VARCHAR(250) NOT NULL
)
GO
/*CREO LA TABLA ITEMS DEVOLUCIONES*/
CREATE TABLE [POLACA_INVERSA].[ITEMS_DEVOLUCIONES]
( 
	ID_DEVOLUCION INT FOREIGN KEY REFERENCES [POLACA_INVERSA].DEVOLUCIONES(ID_DEVOLUCION),
	ID_FACTURA NUMERIC(18,0) FOREIGN KEY REFERENCES [POLACA_INVERSA].FACTURAS(ID_FACTURA),
	PRIMARY KEY (ID_DEVOLUCION,ID_FACTURA)
)
go
/*CREO LA TABLA DE ITEMS_FACTURA*/
CREATE TABLE [POLACA_INVERSA].ITEMS_FACTURAS(
	ID_ITEM NUMERIC(18,0) IDENTITY PRIMARY KEY,
	ID_FACTURA NUMERIC(18,0) FOREIGN KEY REFERENCES POLACA_INVERSA.FACTURAS(ID_FACTURA),
	CANTIDAD INT,
	MONTO FLOAT,
)
GO

/*CREO LA TABLA DE FACTURAS_RENDIDAS*/
CREATE TABLE [POLACA_INVERSA].FACTURAS_RENDIDAS(
	ID_RENDICION NUMERIC(18,0) PRIMARY KEY,
	FEC_RENDICION DATETIME NOT NULL,
	ID_EMPRESA INT NOT NULL FOREIGN KEY REFERENCES POLACA_INVERSA.EMPRESAS(ID_EMPRESA),
	CANT_FAC_REND INT NOT NULL,
	TOTAL_RENDICION FLOAT NOT NULL,
	IMPORTE_COMISION FLOAT NOT NULL,
	PORCENTAJE_COMISION FLOAT NOT NULL,	
)
GO

/*CREO LA TABLA DE ITEMS_RENDICION*/
CREATE TABLE [POLACA_INVERSA].ITEMS_FACTURAS_RENDIDAS(
	ID_RENDICION NUMERIC(18,0) FOREIGN KEY REFERENCES POLACA_INVERSA.FACTURAS_RENDIDAS(ID_RENDICION),
	ID_FACTURA NUMERIC(18,0) FOREIGN KEY REFERENCES POLACA_INVERSA.FACTURAS(ID_FACTURA),
	PRIMARY KEY(ID_RENDICION,ID_FACTURA)
)
GO
--Index

CREATE INDEX Indice_Facturas ON POLACA_INVERSA.FACTURAS(ID_FACTURA)
go

CREATE INDEX Indice_Empresas ON POLACA_INVERSA.EMPRESAS(ID_EMPRESA)
go

CREATE INDEX Indice_Clientes ON POLACA_INVERSA.CLIENTES(ID_CLIENTE)
go
-- Funciones

CREATE FUNCTION POLACA_INVERSA.USUARIO_GET_ID(@usuario VARCHAR(20)) RETURNS INT AS
BEGIN
	RETURN	(SELECT id_usuario
			FROM POLACA_INVERSA.Usuarios
			WHERE username = @usuario)
END
GO

CREATE FUNCTION POLACA_INVERSA.USUARIO_GET_ROLES(@usuarioId int) RETURNS TABLE
AS
	RETURN	(SELECT nombre_rol AS Nombre, R.ID_ROL, habilitado AS Habilitado
				FROM POLACA_INVERSA.ROLES as R
					JOIN POLACA_INVERSA.ROL_USUARIO as R_U ON  R_U.ID_ROL= R.ID_ROL
				WHERE R_U.ID_USUARIO = @usuarioId)
GO

CREATE FUNCTION POLACA_INVERSA.USUARIO_GET_SUCURSALES(@usuarioId int) RETURNS TABLE
AS
	RETURN	(SELECT S.ID_SUCURSAL, S.Codigo_Postal, S.Direccion, S.Nombre, S.ESTADO_SUCURSAL AS Habilitado
				FROM POLACA_INVERSA.SUCURSAL_USUARIO as S_U
					JOIN POLACA_INVERSA.SUCURSALES as S ON  S_U.ID_SUCURSAL= S.ID_SUCURSAL
				WHERE S_U.ID_USUARIO = @usuarioId)
GO

CREATE FUNCTION POLACA_INVERSA.ROL_GET_ACCESOS(@rolId TINYINT) RETURNS TABLE
AS
	RETURN	(	SELECT Id_Acceso
				FROM POLACA_INVERSA.ROL_ACCESOS
				WHERE ID_ROL = @rolId)
GO

CREATE FUNCTION POLACA_INVERSA.ACCESO_GET_NOMBRE(@accesoId TINYINT) RETURNS varchar(200)
AS
BEGIN
	RETURN	(	SELECT Nombre
				FROM POLACA_INVERSA.ACCESOS
				WHERE id_acceso = @accesoId)
END
GO

CREATE FUNCTION POLACA_INVERSA.GET_CLIENTES_CON_FILTROS (@nombre varchar(255),			
														 @apellido varchar(255),
														 @dni numeric(18,0))
RETURNS TABLE
AS 
	RETURN	(SELECT  ID_CLIENTE,
					 APELLIDO as Apellido,
					 NOMBRE as Nombre,
					 DNI as DNI,
					 MAIL as Mail,
					 TELEFONO as Telefono,
					 DIRECCION as Domicilio,
					 NUMERO as Numero,
					 PISO as Piso,
					 DPTO as Dpto,
					 LOCALIDAD as Localidad,
					 CODIGO_POSTAL as "Codigo Postal",
					 FECHA_NACIMIENTO as "Fecha Nacimiento",
					 HABILITADO
			FROM POLACA_INVERSA.CLIENTES
			WHERE (@nombre = '' OR CHARINDEX(@nombre, NOMBRE) > 0) AND
				(@apellido = '' OR CHARINDEX(@apellido, APELLIDO) > 0) AND
				(@dni = 0 OR @dni = DNI))
GO

CREATE FUNCTION POLACA_INVERSA.ES_EMAIL_EXISTENTE(@emailCliente varchar(100))
RETURNS bit
AS
BEGIN
		DECLARE @existe bit
		set @existe = 0 
		IF EXISTS (SELECT ID_CLIENTE FROM POLACA_INVERSA.CLIENTES where MAIL = @emailCliente)
		begin
			set @existe = 1
		end
		return @existe
END;
GO

CREATE FUNCTION POLACA_INVERSA.FUNCIONALIDADES_GET_TABLA_DE_ROL (@Id_Rol TINYINT)
RETURNS TABLE
AS
	RETURN (SELECT A.ID_ACCESO as Id_Acceso, NOMBRE as Nombre
			FROM POLACA_INVERSA.ROL_ACCESOS as R_A
			JOIN POLACA_INVERSA.ACCESOS as A on R_A.ID_ACCESO = A.ID_ACCESO
			where R_A.ID_ROL=@Id_Rol)
GO

CREATE FUNCTION POLACA_INVERSA.FUNCIONALIDADES_GET_TABLA_DE_ROL_DISPONIBLES (@Id_Rol TINYINT)
RETURNS TABLE
AS
	RETURN (SELECT ID_ACCESO as Id_Acceso, NOMBRE as Nombre
	FROM POLACA_INVERSA.ACCESOS
	where ID_ACCESO NOT IN (SELECT A.ID_ACCESO as Id_Acceso
							FROM POLACA_INVERSA.ROL_ACCESOS as R_A
							JOIN POLACA_INVERSA.ACCESOS as A on R_A.ID_ACCESO = A.ID_ACCESO
							where R_A.ID_ROL=@Id_Rol))
GO

CREATE FUNCTION POLACA_INVERSA.GET_EMPRESAS_CON_FILTROS(@nombre varchar(250),
														@cuit numeric(18,0),
														@rubro varchar(250))
RETURNS TABLE
AS
	RETURN (SELECT 	ID_EMPRESA,
					Cuit,
					Nombre,
					Direccion,
					R.DETALLE as Rubro,
					ESTADO_EMPRESA
			FROM POLACA_INVERSA.EMPRESAS as E
				JOIN POLACA_INVERSA.RUBRO as R ON E.ID_RUBRO = R.ID_RUBRO
			WHERE (@nombre = '' OR CHARINDEX(@nombre, NOMBRE) > 0) AND
				(@cuit = 0 OR @cuit = CUIT) AND
				(@rubro = '' OR R.DETALLE = @rubro))

GO

CREATE FUNCTION POLACA_INVERSA.GET_FACTURAS_CON_FILTROS(@numFactura int,
														@dniCliente numeric(18,0),
														@cuitEmpresa numeric(18,0),
														@total numeric(18,2))
RETURNS TABLE
AS
	RETURN (SELECT 	f.ID_FACTURA,
					e.NOMBRE as 'Nombre Empresa',
					e.CUIT as 'Cuit Empresa',
					c.NOMBRE as 'Nombre Cliente',
					c.DNI,
					f.FECHA_ALTA as 'Fecha Alta',
					f.FECHA_VENCIMIENTO as 'Fecha Vencimiento',
					f.TOTAL as Total,
					f.HABILITADO
			FROM POLACA_INVERSA.FACTURAS as f
				JOIN POLACA_INVERSA.EMPRESAS as e ON f.ID_EMPRESA = e.ID_EMPRESA
				JOIN POLACA_INVERSA.CLIENTES as c ON f.ID_CLIENTE = c.ID_CLIENTE
			WHERE 	(@numFactura = 0 OR f.ID_FACTURA = @numFactura) AND
					(@dniCliente = 0 OR c.DNI = @dniCliente) AND
					(@cuitEmpresa = 0 OR e.CUIT = @cuitEmpresa) AND
					(@total = 0 OR f.TOTAL = @total))
GO

CREATE FUNCTION POLACA_INVERSA.FACTURA_ESTACOBRADA(@idFactura int)
RETURNS bit
AS
BEGIN
		DECLARE @existe bit
		set @existe = 0 
		IF EXISTS (SELECT ID_PAGO FROM POLACA_INVERSA.ITEMS_PAGOS where ID_FACTURA = @idFactura)
		begin
			set @existe = 1
		end
		return @existe
END;
GO

CREATE FUNCTION POLACA_INVERSA.FACTURA_GET_DETALLE( @idFactura NUMERIC(18,0))
RETURNS TABLE
AS
	RETURN (SELECT Cantidad, Monto
			FROM POLACA_INVERSA.ITEMS_FACTURAS
			WHERE ID_FACTURA = @idFactura)
	
GO

CREATE FUNCTION POLACA_INVERSA.BUSCAR_FACTURAS_PENDIENTES_DE_RENDICION_EMPRESA (@idEmpresa INT)
RETURNS TABLE
AS
	RETURN (SELECT f.ID_FACTURA,f.ID_CLIENTE,f.ID_EMPRESA,f.TOTAL,f.FECHA_VENCIMIENTO,f.FECHA_ALTA,f.HABILITADO
			FROM [POLACA_INVERSA].FACTURAS f JOIN
			(([POLACA_INVERSA].Pagos p JOIN [POLACA_INVERSA].ITEMS_PAGOS ip on p.ID_PAGO = ip.ID_PAGO) LEFT JOIN 
			([POLACA_INVERSA].Devoluciones d JOIN [POLACA_INVERSA].ITEMS_DEVOLUCIONES id on d.ID_DEVOLUCION = id.ID_DEVOLUCION )
			on ip.ID_FACTURA = id.ID_FACTURA) 
			on f.ID_FACTURA = ip.ID_FACTURA
			where f.ID_FACTURA not in (SELECT ID_FACTURA from [POLACA_INVERSA].ITEMS_FACTURAS_RENDIDAS)
			group by f.ID_FACTURA,f.ID_CLIENTE,f.ID_EMPRESA,f.TOTAL,f.FECHA_VENCIMIENTO,f.FECHA_ALTA,f.HABILITADO
			having (max(d.FECHA_DEVOLUCION) is null OR (max(d.FECHA_DEVOLUCION)<max(p.FECHA_PAGO)))
			and f.ID_EMPRESA = @idEmpresa)
GO

CREATE FUNCTION POLACA_INVERSA.GET_SUCURSAL_CON_FILTROS(@nombre varchar(250),
											  @direccion varchar(250),
											  @codigoPostal numeric(18,0))
RETURNS TABLE
AS
	RETURN (SELECT 	ID_SUCURSAL,
					Nombre,
					Direccion,
					Codigo_Postal,
					Estado_Sucursal as 'Habilitado'
			FROM POLACA_INVERSA.SUCURSALES
			WHERE (@nombre = '' OR CHARINDEX(@nombre, NOMBRE) > 0) AND
				  (@direccion = '' OR CHARINDEX(@direccion, DIRECCION) > 0) AND
				  (@codigoPostal = 0 OR @codigoPostal = CODIGO_POSTAL))
GO

CREATE FUNCTION POLACA_INVERSA.SUCURSAL_ESTA_HABILITADA(@id int)
RETURNS bit
AS
BEGIN
	RETURN (SELECT ESTADO_SUCURSAL
			FROM POLACA_INVERSA.SUCURSALES
			where ID_SUCURSAL = @id)
END
GO

CREATE FUNCTION POLACA_INVERSA.GET_MEDIOS_PAGOS()
RETURNS TABLE
AS
	RETURN (SELECT ID_MEDIO, Descripcion
			FROM POLACA_INVERSA.MEDIOS_PAGO)
GO

CREATE FUNCTION POLACA_INVERSA.FACTURA_EXISTE(@idFactura int)
RETURNS bit
AS
BEGIN
		DECLARE @existe bit
		set @existe = 0 
		IF EXISTS (SELECT ID_FACTURA FROM POLACA_INVERSA.FACTURAS where ID_FACTURA = @idFactura)
		begin
			set @existe = 1
		end
		return @existe
END;
GO

CREATE FUNCTION POLACA_INVERSA.FACTURA_ESTA_HABILITADA(@idFactura int)
RETURNS bit
AS
BEGIN
	RETURN (SELECT HABILITADO
			FROM POLACA_INVERSA.FACTURAS
			where ID_FACTURA = @idFactura)
END
GO

CREATE FUNCTION POLACA_INVERSA.FACTURA_ES_DE_EMPRESA(@idFactura int, @idEmpresa int)
RETURNS bit
AS
BEGIN
	DECLARE @resultado bit
	set @resultado = 0 
	IF EXISTS (SELECT ID_FACTURA FROM POLACA_INVERSA.FACTURAS where ID_FACTURA = @idFactura AND ID_EMPRESA = @idEmpresa)
	begin
		set @resultado = 1
	end
	return @resultado
END
GO

CREATE FUNCTION POLACA_INVERSA.FACTURA_ES_DE_CLIENTE(@idFactura int, @idCliente int)
RETURNS bit
AS
BEGIN
	DECLARE @resultado bit
	set @resultado = 0 
	IF EXISTS (SELECT ID_FACTURA FROM POLACA_INVERSA.FACTURAS where ID_FACTURA = @idFactura AND ID_CLIENTE = @idCliente)
	begin
		set @resultado = 1
	end
	return @resultado
END
GO

CREATE FUNCTION POLACA_INVERSA.GET_FECHA_VENCIMIENTO_FACTURA(@idFactura int)
RETURNS DATETIME
AS
BEGIN
	RETURN (SELECT FECHA_VENCIMIENTO
			FROM POLACA_INVERSA.FACTURAS
			where ID_FACTURA = @idFactura)
END
GO

CREATE FUNCTION POLACA_INVERSA.FACTURA_VALIDAR_IMPORTE(@idFactura int, @importe Numeric(18,2))
RETURNS bit
AS
BEGIN
	DECLARE @resultado bit
	set @resultado = 0 
	IF EXISTS (SELECT ID_FACTURA FROM POLACA_INVERSA.FACTURAS where ID_FACTURA = @idFactura AND TOTAL = @importe)
	begin
		set @resultado = 1
	end
	return @resultado
END
GO

CREATE FUNCTION POLACA_INVERSA.BUSCAR_FACTURAS_A_RENDIR(@idEmpresa INT, @fechaRendicion DATETIME)
RETURNS TABLE
AS
	RETURN (
		SELECT f.ID_FACTURA as 'Numero Factura', e.NOMBRE as 'Nombre Empresa', c.NOMBRE as 'Nombre Cliente', f.TOTAL as 'Monto', p.FECHA_PAGO as 'Fecha Pago'
		FROM POLACA_INVERSA.FACTURAS f
			JOIN POLACA_INVERSA.ITEMS_PAGOS ip ON f.ID_FACTURA = ip.ID_FACTURA
			JOIN POLACA_INVERSA.PAGOS p ON ip.ID_PAGO = p.ID_PAGO
			LEFT JOIN POLACA_INVERSA.ITEMS_DEVOLUCIONES id ON id.ID_FACTURA = f.ID_FACTURA
			LEFT JOIN POLACA_INVERSA.DEVOLUCIONES d ON id.ID_DEVOLUCION = d.ID_DEVOLUCION
			JOIN POLACA_INVERSA.EMPRESAS e ON f.ID_EMPRESA = e.ID_EMPRESA
			JOIN POLACA_INVERSA.CLIENTES c ON f.ID_CLIENTE = c.ID_CLIENTE
		WHERE f.ID_EMPRESA = @idEmpresa
			AND f.ID_FACTURA not in (SELECT ID_FACTURA FROM POLACA_INVERSA.ITEMS_FACTURAS_RENDIDAS)
			AND (p.FECHA_PAGO >= d.FECHA_DEVOLUCION OR d.FECHA_DEVOLUCION IS NULL) 
			AND @fechaRendicion >= p.FECHA_PAGO
			)
GO

CREATE FUNCTION POLACA_INVERSA.VALIDAR_RENDICION_FECHAxEMPRESA(@idEmpresa INT, @fechaRendicion DATETIME)
RETURNS bit
AS
BEGIN
	DECLARE @resultado bit
	set @resultado = 0 
	IF(month(@fechaRendicion) not in (select month(FEC_RENDICION) FROM POLACA_INVERSA.FACTURAS_RENDIDAS WHERE ID_EMPRESA = @idEmpresa))
	begin
		set @resultado = 1
	end
	return @resultado
END
GO

CREATE FUNCTION POLACA_INVERSA.GET_FACTURAS_PAGAS_CON_FILTROS(@dniCliente numeric(18,0),
															  @fechaDesde DATETIME,
															  @fechaHasta DATETIME)
RETURNS TABLE
AS
	RETURN (SELECT c.NOMBRE as 'Cliente', f.ID_FACTURA as 'Factura',f.TOTAL as 'Total',f.FECHA_VENCIMIENTO as 'Fecha Vencimiento'
				FROM [POLACA_INVERSA].FACTURAS f
				 JOIN [POLACA_INVERSA].ITEMS_PAGOS ip on f.ID_FACTURA = ip.ID_FACTURA
				 JOIN [POLACA_INVERSA].PAGOS p on p.ID_PAGO = ip.ID_PAGO
				 LEFT JOIN [POLACA_INVERSA].ITEMS_DEVOLUCIONES i_d on  i_d.ID_FACTURA = f.ID_FACTURA
				 LEFT JOIN [POLACA_INVERSA].DEVOLUCIONES d on d.ID_DEVOLUCION = i_d.ID_DEVOLUCION
				 JOIN [POLACA_INVERSA].CLIENTES c on f.ID_CLIENTE = c.ID_CLIENTE
				 where f.ID_FACTURA not in (select if_r.ID_FACTURA 
											from POLACA_INVERSA.ITEMS_FACTURAS_RENDIDAS if_r
											JOIN [POLACA_INVERSA].FACTURAS_RENDIDAS fr on if_r.ID_RENDICION=fr.ID_RENDICION)
						and i_d.ID_DEVOLUCION is NULL
						and c.DNI = @dniCliente
						and p.FECHA_PAGO BETWEEN @fechaDesde AND @fechaHasta)
GO

-- Fin de Funciones
	
-- Procedimientos

CREATE PROC POLACA_INVERSA.SPLOGIN
	@usuario varchar(20),
	@contrasenia varbinary(256)
AS
BEGIN

	DECLARE @pass varbinary(256), @intentos TINYINT, @habilitado BIT

	SELECT @pass=password, @intentos=intentos, @habilitado = habilitado
	FROM POLACA_INVERSA.USUARIOS
	WHERE username = @usuario

	IF (@pass IS NULL) 
	BEGIN
		RAISERROR ('Usuario inexistente!', 16, 1)
		RETURN
	END

	IF (@habilitado = 0) 
	BEGIN
		RAISERROR ('Usuario inhabilitado!', 16, 1)
		RETURN
	END

	IF (@pass <> @contrasenia) 
	BEGIN
		UPDATE POLACA_INVERSA.USUARIOS SET intentos = @intentos + 1
		WHERE username = @usuario
		DECLARE @error varchar(100)= 'Contrasenia Incorrecta! Intentos restantes: ' + str(2 - @intentos)
		RAISERROR (@error, 16, 1)

		IF(@intentos = 2)
		BEGIN
			UPDATE POLACA_INVERSA.USUARIOS SET habilitado = 0
			WHERE username = @usuario
		END
		RETURN
	END

	UPDATE POLACA_INVERSA.USUARIOS SET intentos = 0
	WHERE username = @usuario

END
GO

CREATE PROC POLACA_INVERSA._INHABILITAR 
@id int
AS
BEGIN
	UPDATE POLACA_INVERSA.CLIENTES SET HABILITADO=0 where ID_CLIENTE=@id 
END
GO

CREATE PROC POLACA_INVERSA._HABILITAR 
@id int
AS
BEGIN
	UPDATE POLACA_INVERSA.CLIENTES SET HABILITADO=1 where ID_CLIENTE=@id 
END
GO

CREATE PROC POLACA_INVERSA.CLIENTE_UPDATE(	@id INT,
											@nombre VARCHAR(255),
											@apellido VARCHAR(255),
											@dni NUMERIC(18,0),
											@mail VARCHAR(255),
											@telefono NUMERIC(18,0),
											@domicilio VARCHAR(255),
											@numero INT,
											@piso INT,
											@dpto varchar(10),
											@localidad varchar(50),
											@fechaNac DATETIME,
											@codigoPostal NUMERIC(18,0),
											@habilitado BIT)
AS
BEGIN
		
	UPDATE POLACA_INVERSA.CLIENTES SET	HABILITADO = @habilitado,
										CODIGO_POSTAL = @codigoPostal,
										NOMBRE = @nombre, 
										APELLIDO = @apellido,
										DNI = @dni,
										FECHA_NACIMIENTO = @fechaNac,
										MAIL = @mail,
										TELEFONO = @telefono,
										DIRECCION = @domicilio,
										NUMERO =@numero,
										PISO =@piso,
										DPTO=@dpto,
										LOCALIDAD=@localidad
	WHERE ID_CLIENTE = @id
END
GO

CREATE PROC POLACA_INVERSA.CLIENTE_NUEVO(	@nombre VARCHAR(255),
											@apellido VARCHAR(255),
											@dni NUMERIC(18,0),
											@mail VARCHAR(255),
											@telefono NUMERIC(18,0),
											@domicilio VARCHAR(255),
											@numero INT,
											@piso INT,
											@dpto varchar(10),
											@localidad varchar(50),
											@fechaNac DATETIME,
											@codigoPostal NUMERIC(18,0),
											@habilitado BIT)
AS
BEGIN
		
	INSERT INTO POLACA_INVERSA.CLIENTES VALUES (@dni,
												@nombre, 
												@apellido,
												@mail,
												@telefono,
												@domicilio,
												@numero,
												@piso,
												@dpto,
												@localidad,
												@codigoPostal,
												@fechaNac,
												@habilitado)
END
GO

CREATE TYPE POLACA_INVERSA.TABLA_ROL_X_ACCESO AS TABLE(
    idAcceso TINYINT,
	nombre VARCHAR(255)
	--habilitado BIT
)
GO

CREATE PROC POLACA_INVERSA.ROL_INHABILITAR(@rol TINYINT) AS
BEGIN
	DELETE POLACA_INVERSA.ROL_USUARIO 
	WHERE ID_ROL = @rol AND ID_USUARIO <> 1

	UPDATE POLACA_INVERSA.Rol SET HABILITADO = 0
	WHERE ID_ROL = @rol
END
GO

CREATE PROC POLACA_INVERSA.ROL_UPDATE(	@id TINYINT,
										@nombre VARCHAR(200),
										@funcionalidades TABLA_ROL_X_ACCESO READONLY,
										@habilitado BIT)
AS
BEGIN
	IF (@habilitado = 0)
	BEGIN
		EXEC POLACA_INVERSA.ROL_INHABILITAR @id
	END

	UPDATE POLACA_INVERSA.ROLES SET	NOMBRE_ROL = @nombre,
									HABILITADO = @habilitado
	
	WHERE ID_ROL = @id

	DELETE POLACA_INVERSA.ROL_ACCESOS
	WHERE ID_ROL = @id

	INSERT POLACA_INVERSA.ROL_ACCESOS
		SELECT @id, idAcceso
		FROM @funcionalidades
		--WHERE habilitado = 1
END
GO

CREATE PROC POLACA_INVERSA.ROL_NUEVO(	@nombre VARCHAR(200),
										@funcionalidades TABLA_ROL_X_ACCESO READONLY,
										@habilitado BIT)
AS
BEGIN
	INSERT POLACA_INVERSA.ROLES VALUES
		(@nombre, @habilitado)

	DECLARE @rol TINYINT = SCOPE_IDENTITY()

	INSERT POLACA_INVERSA.ROL_ACCESOS
		SELECT @rol, idAcceso
		FROM @funcionalidades
		--WHERE habilitado = 1
END
GO

CREATE PROC POLACA_INVERSA.EMPRESA_UPDATE(	@id INT,
											@nombre varchar(200),
											@cuit numeric(18),
											@direccion varchar(250),
											@rubro varchar(250),
											@habilitado BIT)
AS
BEGIN
	IF (@habilitado = 0)
	BEGIN
		EXEC POLACA_INVERSA.INHABILITAR_EMPRESA @id
	END
	
	UPDATE POLACA_INVERSA.EMPRESAS SET	CUIT = @cuit,
									ID_RUBRO = (SELECT ID_RUBRO FROM RUBRO WHERE DETALLE = @rubro),
									NOMBRE = @nombre,
									DIRECCION = @direccion,
									ESTADO_EMPRESA = @habilitado
	WHERE ID_EMPRESA = @id

END
GO

CREATE PROC POLACA_INVERSA.EMPRESA_NUEVA(	@nombre VARCHAR(255),
											@cuit NUMERIC(18,0),
											@direccion VARCHAR(255),
											@rubro VARCHAR(250),
											@habilitado BIT)
AS
BEGIN
		
	INSERT INTO POLACA_INVERSA.EMPRESAS (NOMBRE,CUIT,DIRECCION,ID_RUBRO,ESTADO_EMPRESA)
	VALUES (@nombre, 
			@cuit, 
			@direccion, 
			(SELECT ID_RUBRO FROM RUBRO WHERE DETALLE = @rubro), 
			@habilitado)
END
GO

CREATE PROC POLACA_INVERSA.INHABILITAR_EMPRESA(	@id int)
AS
BEGIN
	UPDATE POLACA_INVERSA.EMPRESAS SET ESTADO_EMPRESA=0 WHERE ID_EMPRESA=@id
END
GO

CREATE PROC POLACA_INVERSA.HABILITAR_EMPRESA (@id int)
AS
BEGIN
	UPDATE POLACA_INVERSA.EMPRESAS SET ESTADO_EMPRESA=1 WHERE ID_EMPRESA = @id
END
GO

CREATE PROC POLACA_INVERSA.FACTURA_NUEVA(	@numeroFactura NUMERIC(18,0),
											@idEmpresa NUMERIC(18,0),
											@dniCliente NUMERIC(18,0),
											@fechaAlta DATETIME,
											@fechaVencimiento DATETIME,
											@habilitado BIT)
AS
BEGIN
		
	INSERT INTO POLACA_INVERSA.FACTURAS (ID_FACTURA,ID_EMPRESA,ID_CLIENTE,FECHA_ALTA,FECHA_VENCIMIENTO, HABILITADO)
	VALUES (@numeroFactura, 
			@idEmpresa, 
			(SELECT ID_CLIENTE FROM CLIENTES WHERE DNI = @dniCliente), 
			@fechaAlta,
			@fechaVencimiento,
			@habilitado)
END
GO

CREATE TYPE POLACA_INVERSA.TABLA_ITEMS_FACTURA AS TABLE(
    cantidad INT,
	monto FLOAT
)
GO

CREATE PROC POLACA_INVERSA.DETALLE_FACTURA_NUEVO(	@idFactura NUMERIC(18,0),
													@total NUMERIC(18,2),
													@items TABLA_ITEMS_FACTURA READONLY)
AS
BEGIN
	
	UPDATE POLACA_INVERSA.FACTURAS SET TOTAL=@total WHERE ID_FACTURA=@idFactura
	
	DELETE FROM POLACA_INVERSA.ITEMS_FACTURAS WHERE ID_FACTURA=@idFactura
	
	INSERT POLACA_INVERSA.ITEMS_FACTURAS (ID_FACTURA,CANTIDAD,MONTO)
		SELECT @idFactura, cantidad, monto
		FROM @items
END
GO

CREATE PROC POLACA_INVERSA.FACTURA_UPDATE( @idFactura NUMERIC(18,0),
											@idEmpresa NUMERIC(18,0),
											@dniCliente NUMERIC(18,0),
											@fechaAlta DATETIME,
											@fechaVencimiento DATETIME,
											@habilitado BIT)
AS
BEGIN
	UPDATE POLACA_INVERSA.FACTURAS SET 	ID_EMPRESA = @idEmpresa,
										ID_CLIENTE = (SELECT ID_CLIENTE FROM CLIENTES WHERE DNI = @dniCliente),
										FECHA_ALTA = @fechaAlta,
										FECHA_VENCIMIENTO = @fechaVencimiento,
										HABILITADO = @habilitado
	WHERE ID_FACTURA = @idFactura
END
GO

CREATE PROC POLACA_INVERSA.INHABILITAR_FACTURA(	@idFactura Numeric(18,0))
AS
BEGIN
	UPDATE POLACA_INVERSA.FACTURAS SET HABILITADO=0 WHERE ID_FACTURA=@idFactura
END
GO

CREATE PROC POLACA_INVERSA.SUCURSAL_UPDATE( @idSucursal INT,
											@nombre VARCHAR(250),
											@direccion VARCHAR(250),
											@codigoPostal NUMERIC(18,0),
											@habilitado BIT)
AS
BEGIN
	UPDATE POLACA_INVERSA.SUCURSALES SET 	NOMBRE = @nombre,
												DIRECCION = @direccion,
												CODIGO_POSTAL = @codigoPostal,
												ESTADO_SUCURSAL = @habilitado
	WHERE ID_SUCURSAL = @idSucursal
END
GO

CREATE PROC POLACA_INVERSA.SUCURSAL_NUEVO(	
											@nombre VARCHAR(250),
											@direccion VARCHAR(250),
											@codigoPostal NUMERIC(18,0),
											@habilitado BIT)
AS
BEGIN
		INSERT INTO POLACA_INVERSA.SUCURSALES (NOMBRE,DIRECCION,CODIGO_POSTAL,ESTADO_SUCURSAL)
		VALUES (@nombre, @direccion, @codigoPostal, @habilitado)
END
GO

CREATE PROC POLACA_INVERSA.INHABILITAR_SUCURSAL(@id int)
AS
BEGIN
		UPDATE POLACA_INVERSA.SUCURSALES SET ESTADO_SUCURSAL=0 WHERE ID_SUCURSAL=@id
END
GO

CREATE PROC POLACA_INVERSA.HABILITAR_SUCURSAL(@id int)
AS
BEGIN
		UPDATE POLACA_INVERSA.SUCURSALES SET ESTADO_SUCURSAL=1 WHERE ID_SUCURSAL=@id
END
GO

CREATE TYPE POLACA_INVERSA.TABLA_ITEMS_PAGO AS TABLE(
    idFactura Numeric(18,0),
	nombreEmpresa VARCHAR(250),
	fechaVencimiento DATETIME,
	importe float
)
GO

CREATE PROC POLACA_INVERSA.PAGO_NUEVO(	@idPago INT OUT,
										@idSucursal INT,
										@idCliente INT,
										@medioPago INT,
										@fechaPago DATETIME,
										@total NUMERIC(18,2),
										@facturas TABLA_ITEMS_PAGO READONLY)
AS
BEGIN
	SET @idPago = 0
	SELECT top 1 @idPago = ID_PAGO + 1 from [POLACA_INVERSA].[PAGOS] ORDER BY ID_PAGO DESC
	INSERT INTO POLACA_INVERSA.PAGOS (ID_PAGO,ID_SUCURSAL,ID_CLIENTE,MEDIO_PAGO,FECHA_PAGO,TOTAL_PAGO)
	VALUES (@idPago, @idSucursal, @idCliente, @medioPago, @fechaPago, @total)
	
	INSERT POLACA_INVERSA.ITEMS_PAGOS (ID_PAGO,ID_FACTURA)
	SELECT @idPago,idFactura 
	FROM @facturas
	
END
GO

CREATE TYPE POLACA_INVERSA.TABLA_ITEMS_RENDICION AS TABLE(
    idFactura Numeric(18,0),
	nombreEmpresa VARCHAR(250),
	nombreCliente VARCHAR(250),
	importe float,
	fechaPago DATETIME
)
GO

CREATE PROC POLACA_INVERSA.RENDIR_FACTURAS(	@porcentajeComision FLOAT,
											@comision FLOAT,
											@total FLOAT,
											@cantidadfacturas INT,
											@idEmpresa INT,
											@fechaRendicion DATETIME,
											@itemsFacturas TABLA_ITEMS_RENDICION READONLY)
AS
BEGIN
	Declare @idRendicion NUMERIC(18,0)
	SET @idRendicion = 0
	SELECT top 1 @idRendicion = ID_RENDICION + 1 from [POLACA_INVERSA].[FACTURAS_RENDIDAS] ORDER BY ID_RENDICION DESC
	INSERT INTO POLACA_INVERSA.FACTURAS_RENDIDAS (ID_RENDICION,FEC_RENDICION,ID_EMPRESA,CANT_FAC_REND,TOTAL_RENDICION,IMPORTE_COMISION,PORCENTAJE_COMISION)
	VALUES (@idRendicion, @fechaRendicion, @idEmpresa, @cantidadfacturas, @total,@comision,@porcentajeComision)
	
	INSERT POLACA_INVERSA.ITEMS_FACTURAS_RENDIDAS(ID_RENDICION,ID_FACTURA)
	SELECT @idRendicion,idFactura 
	FROM @itemsFacturas
	
END
GO

CREATE TYPE POLACA_INVERSA.TABLA_ITEMS_DEVOLUCION AS TABLE(
	nombreCliente VARCHAR(250),
	idFactura Numeric(18,0),
	total float,
	fechaVencimiento DATETIME
)
GO

CREATE PROC POLACA_INVERSA.DEVOLUCION_NUEVA(	@motivoDevolucion VARCHAR(250),
												@fechaDevolucion DATETIME,
												@facturas TABLA_ITEMS_DEVOLUCION READONLY)
AS
BEGIN
	DECLARE @idDevolucion INT
	SET @idDevolucion = 0
	SELECT top 1 @idDevolucion = ID_DEVOLUCION + 1 from [POLACA_INVERSA].[DEVOLUCIONES] ORDER BY ID_DEVOLUCION DESC
	INSERT INTO POLACA_INVERSA.DEVOLUCIONES(ID_DEVOLUCION,MOTIVO_DEVOLUCION,FECHA_DEVOLUCION)
	VALUES (@idDevolucion, @motivoDevolucion, @fechaDevolucion)
	
	INSERT POLACA_INVERSA.ITEMS_DEVOLUCIONES (ID_DEVOLUCION,ID_FACTURA)
	SELECT @idDevolucion,idFactura 
	FROM @facturas
END
GO

-- Fin de Procedimientos

/* -- MIGRACION -- */

/*Para ejecutar las procedures una vez creadas, ejecutar: [POLACA_INVERSA].NombreProcedure */

--CLIENTES
CREATE PROCEDURE [POLACA_INVERSA].MigrarClientes
AS
BEGIN

	INSERT INTO [POLACA_INVERSA].CLIENTES(NOMBRE, APELLIDO, DNI, MAIL, DIRECCION,NUMERO,PISO,DPTO,LOCALIDAD, CODIGO_POSTAL, FECHA_NACIMIENTO)
	SELECT DISTINCT "Cliente-Nombre", "Cliente-Apellido", "Cliente-Dni", Cliente_Mail, Cliente_Direccion,0,0,'-','No Definido', Cliente_Codigo_Postal, "Cliente-Fecha_Nac"
	FROM gd_esquema.Maestra

END;
GO

--RUBROS
CREATE PROCEDURE [POLACA_INVERSA].MigrarRubros
AS
BEGIN
	
	INSERT INTO [POLACA_INVERSA].RUBRO(ID_RUBRO,DETALLE)
	SELECT DISTINCT Empresa_Rubro,'Nombre Empresa'
	FROM gd_esquema.Maestra

END;
GO

--EMPRESAS
CREATE PROCEDURE [POLACA_INVERSA].MigrarEmpresas
AS
BEGIN

	INSERT INTO [POLACA_INVERSA].EMPRESAS(CUIT, ID_RUBRO, NOMBRE, DIRECCION)
	SELECT DISTINCT REPLACE (Empresa_Cuit,'-',''), Empresa_Rubro ,Empresa_Nombre, Empresa_Direccion
	FROM gd_esquema.Maestra
	
END;
GO

--FACTURAS

CREATE PROCEDURE [POLACA_INVERSA].MigrarFactura
AS
BEGIN

	--SET IDENTITY_INSERT POLACA_INVERSA.FACTURAS ON

	INSERT POLACA_INVERSA.FACTURAS(ID_FACTURA, ID_EMPRESA, ID_CLIENTE, FECHA_ALTA, FECHA_VENCIMIENTO, TOTAL)
	SELECT DISTINCT Nro_Factura, e.ID_EMPRESA, c.ID_CLIENTE, Factura_Fecha, Factura_Fecha_Vencimiento, Factura_total
	FROM gd_esquema.Maestra m
		JOIN POLACA_INVERSA.EMPRESAS e ON (SELECT REPLACE (m.Empresa_Cuit,'-','')) = e.CUIT
		JOIN POLACA_INVERSA.CLIENTES c ON m.[Cliente-Dni] = c.DNI
	WHERE Nro_Factura IS NOT NULL
	GROUP BY Nro_Factura, e.ID_EMPRESA, c.ID_CLIENTE, Factura_Fecha, Factura_Fecha_Vencimiento, Factura_total

	--SET IDENTITY_INSERT POLACA_INVERSA.FACTURAS OFF
END;
GO

--Sucursales

CREATE PROCEDURE [POLACA_INVERSA].MigrarSucursales
AS
BEGIN
	INSERT INTO [POLACA_INVERSA].SUCURSALES (CODIGO_POSTAL,NOMBRE,DIRECCION,ESTADO_SUCURSAL) 
	SELECT DISTINCT Sucursal_Codigo_Postal,Sucursal_Nombre, Sucursal_Direcci√≥n,1 
	FROM [gd_esquema].[Maestra] where Sucursal_Codigo_Postal is not null
	
	--Se agrega Sucursal adicional
	INSERT INTO [POLACA_INVERSA].SUCURSALES 
	VALUES ('Sucursal Lanus','Hipolito Yrigoyen 3500',1824,1)
	
	--Se agrega relacion Usuario Admin Sucursales
	INSERT INTO POLACA_INVERSA.SUCURSAL_USUARIO (ID_USUARIO, ID_SUCURSAL) 
	VALUES (1,1)
	
	INSERT INTO POLACA_INVERSA.SUCURSAL_USUARIO (ID_USUARIO, ID_SUCURSAL) 
	VALUES (1,2)
END;
GO

--ITEM FACTURAS
CREATE PROCEDURE [POLACA_INVERSA].MigrarItemFactura
AS
BEGIN
	INSERT POLACA_INVERSA.ITEMS_FACTURAS (ID_FACTURA,CANTIDAD,MONTO)
	SELECT DISTINCT f.ID_FACTURA , m.ItemFactura_Cantidad, m.ItemFactura_Monto
	FROM POLACA_INVERSA.FACTURAS f
		JOIN gd_esquema.Maestra m ON f.ID_FACTURA = m.Nro_Factura
END;
GO

CREATE PROCEDURE [POLACA_INVERSA].MigrarMediosDePagos
AS
BEGIN
	INSERT INTO [POLACA_INVERSA].MEDIOS_PAGO (DESCRIPCION)
	SELECT DISTINCT FormaPagoDescripcion
	FROM gd_esquema.Maestra where FormaPagoDescripcion IS NOT NULL
END;
GO

--ITEMS PAGOS e ITEMS PAGOS
CREATE PROCEDURE [POLACA_INVERSA].MigrarPagos
AS
BEGIN
	INSERT INTO [POLACA_INVERSA].PAGOS (ID_PAGO, FECHA_PAGO, TOTAL_PAGO, MEDIO_PAGO, ID_SUCURSAL, ID_CLIENTE)
	SELECT DISTINCT [Pago_nro],[Pago_Fecha],[Total],mp.ID_MEDIO,s.ID_SUCURSAL, c.ID_CLIENTE
	FROM gd_esquema.Maestra m
		JOIN POLACA_INVERSA.MEDIOS_PAGO mp ON mp.DESCRIPCION = m.FormaPagoDescripcion
		JOIN POLACA_INVERSA.SUCURSALES s ON s.CODIGO_POSTAL = m.Sucursal_Codigo_Postal
		JOIN POLACA_INVERSA.CLIENTES c ON c.DNI = m.[Cliente-Dni]	
	where Pago_nro IS NOT NULL
	
	INSERT [POLACA_INVERSA].ITEMS_PAGOS (ID_PAGO,ID_FACTURA)
	SELECT DISTINCT m.Pago_nro, m.Nro_Factura
	FROM POLACA_INVERSA.PAGOS p
		JOIN gd_esquema.Maestra m ON p.ID_PAGO = m.Pago_nro

END;
GO

--RENDICION
CREATE PROCEDURE [POLACA_INVERSA].MigrarRendicion
AS
BEGIN
	
	INSERT INTO [POLACA_INVERSA].FACTURAS_RENDIDAS (ID_RENDICION, FEC_RENDICION, ID_EMPRESA, CANT_FAC_REND, TOTAL_RENDICION, IMPORTE_COMISION, PORCENTAJE_COMISION)
	SELECT DISTINCT m.[Rendicion_Nro], m.[Rendicion_Fecha], e.ID_EMPRESA, 1, m.Factura_Total, m.ItemRendicion_Importe, 10.00
	FROM gd_esquema.Maestra m 
		JOIN POLACA_INVERSA.EMPRESAS e ON e.CUIT = REPLACE (m.Empresa_Cuit,'-','')
	where m.Rendicion_Nro IS NOT NULL
	
	INSERT INTO [POLACA_INVERSA].ITEMS_FACTURAS_RENDIDAS (ID_RENDICION, ID_FACTURA)
	SELECT DISTINCT m.[Rendicion_Nro], m.[Nro_Factura]
	FROM gd_esquema.Maestra m 
		JOIN POLACA_INVERSA.FACTURAS_RENDIDAS fr ON m.Rendicion_Nro = fr.ID_RENDICION
	where m.Rendicion_Nro IS NOT NULL
	order by Rendicion_Nro
END;
GO

--Usuario Administrador

INSERT INTO POLACA_INVERSA.USUARIOS (USERNAME,PASSWORD,HABILITADO,INTENTOS)
	VALUES ('admin', HASHBYTES('SHA2_256', 'w23e'), 1, 0),
		   ('cobrador', HASHBYTES('SHA2_256', 'cobrador'), 1, 0)
	
--Rol

INSERT POLACA_INVERSA.ROLES (nombre_rol, habilitado)
VALUES	('Administrador', 1),
		('Cobrador', 1)
		
--ACCESOS
INSERT POLACA_INVERSA.ACCESOS (nombre)
VALUES	('ABM de Rol'),
		('ABM de Cliente'),
		('ABM de Empresa'),
		('ABM de Factura'),
		('ABM de Sucursal'),
		('Registro de Pago'),
		('Registro de Rendicion'),
		('Devoluciones'),
		('Listado Estadistico')

--Accesos x Rol
INSERT POLACA_INVERSA.ROL_ACCESOS (id_rol, id_acceso)
	VALUES (1, 1),(1,2), (1,3),(1,4),(1,5),(1,6),(1,7),(1,8),(2,2),(2,3),(2,5)

-- Rol_X_Usuario

INSERT POLACA_INVERSA.ROL_USUARIO (id_usuario, id_rol)
VALUES (1,1),(1,2)

EXEC [POLACA_INVERSA].MigrarClientes
go

EXEC [POLACA_INVERSA].MigrarRubros
go

EXEC [POLACA_INVERSA].MigrarEmpresas
go

EXEC [POLACA_INVERSA].MigrarFactura
go

EXEC [POLACA_INVERSA].MigrarItemFactura
go

EXEC [POLACA_INVERSA].MigrarSucursales
go

EXEC [POLACA_INVERSA].MigrarMediosDePagos
go

EXEC [POLACA_INVERSA].MigrarPagos
go

EXEC [POLACA_INVERSA].MigrarRendicion
go
